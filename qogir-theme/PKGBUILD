_gitname="qogir-theme"
_pkgname="qogir-gtk-theme"
pkgname="$_pkgname-git"
pkgver=2024.05.22.r7.gf3589c6
pkgrel=1
pkgdesc='Qogir is a flat Design theme for GTK'
arch=('any')
url='https://github.com/vinceliuice/Qogir-theme'
license=('GPL3')

depends=(
  'gtk-engine-murrine'
  'gtk-engines'
  'gtk3'
  'gtk4'
)
makedepends=(
  'git'
  'sassc'
)

optdepends=(
  'qogir-icon-theme: Matching icon theme'
)

provides=('qogir-gtk-theme')
conflicts=('qogir-gtk-theme')

source=(
  "$_gitname"::"git+$url"
)
sha256sums=(
  'SKIP'
)

_pkgsrc="$_gitname"
pkgver() {
  cd "$srcdir/$_pkgsrc"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "$srcdir/$_pkgsrc"
  cd src/xfwm4
  sed -i -e 's|show_app_icon=false|show_app_icon=true|' themerc
  sed -i -e 's|show_app_icon=false|show_app_icon=true|' themerc-Light
}

package() {
  cd "$srcdir/$_pkgsrc"

  install -dm755 "$pkgdir/usr/share/themes"
  
  ./install.sh \
    --dest "${pkgdir}/usr/share/themes" \
    --icon arch \
    --libadwaita 
}
